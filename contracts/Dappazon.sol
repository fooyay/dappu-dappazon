// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.30;

contract Dappazon {
    address public owner;

    struct Item {
        uint256 id;
        string name;
        string category;
        string image;
        uint256 cost;
        uint256 rating;
        uint256 stock;
    }

    struct Order {
        uint256 time;
        Item item;
    }

    mapping(uint256 => Item) public items;
    mapping(address => uint256) public orderCount;
    mapping(address => mapping(uint256 => Order)) public orders;

    event Listed(uint256 id, string name, string category, string image, uint256 cost, uint256 rating, uint256 stock);
    event Bought(address buyer, uint256 orderId, uint256 itemId);

    modifier onlyOwner() {
        require(msg.sender == owner, "Only owner can call this function");
        _;
    }

    constructor() {
        owner = msg.sender;
    }

    // list products
    function list(
        uint256 _id,
        string memory _name,
        string memory _category,
        string memory _image,
        uint256 _cost,
        uint256 _rating,
        uint256 _stock
    ) public onlyOwner {
        // create item struct
        Item memory item = Item(_id, _name, _category, _image, _cost, _rating, _stock);
        // save struct to blockchain
        items[_id] = item;
        // emit event
        emit Listed(_id, _name, _category, _image, _cost, _rating, _stock);
    }

    // buy products
    function buy(uint256 _id) public payable {
        // // fetch item
        Item memory item = items[_id];

        // check enough ether sent
        require(msg.value >= item.cost, "Not enough ether sent");

        // check item is in stock
        require(item.stock > 0, "Item is out of stock");

        // create an order
        Order memory order = Order(block.timestamp, item);

        // save order to blockchain
        orderCount[msg.sender]++; // <-- order ID
        orders[msg.sender][orderCount[msg.sender]] = order;

        // subtract stock
        items[_id].stock = item.stock - 1;

        // emit event
        emit Bought(msg.sender, orderCount[msg.sender], item.id);
    }

    // withdraw fuunds
    function withdraw() public onlyOwner {
        (bool success,) = owner.call{value: address(this).balance}("");
        require(success, "Withdraw failed");
    }
}
